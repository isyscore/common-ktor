buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"

    }
}

apply plugin: "java"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "org.jetbrains.kotlin.plugin.serialization"
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'com.github.isyscore'
version '2.2.1'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    api "com.github.isyscore:common-jvm:2.2.0"
    api "io.ktor:ktor-server-core-jvm:$ktor_version"
    api "io.ktor:ktor-server-cors-jvm:$ktor_version"
    api "io.ktor:ktor-server-auth-jvm:$ktor_version"
    api "io.ktor:ktor-server-content-negotiation-jvm:$ktor_version"
    api "io.ktor:ktor-serialization-kotlinx-json-jvm:$ktor_version"
    api "io.ktor:ktor-serialization-gson-jvm:$ktor_version"
    api "io.ktor:ktor-serialization-jackson:$ktor_version"
    api "io.ktor:ktor-server-resources:$ktor_version"
    api "io.ktor:ktor-server-tomcat-jvm:$ktor_version"
    api "io.ktor:ktor-server-netty-jvm:$ktor_version"
    api "io.ktor:ktor-server-config-yaml:$ktor_version"
    api "io.ktor:ktor-server-sessions-jvm:$ktor_version"
    api "io.ktor:ktor-server-default-headers-jvm:$ktor_version"
    api "io.ktor:ktor-server-http-redirect-jvm:$ktor_version"
    api "io.ktor:ktor-server-partial-content-jvm:$ktor_version"
    api "io.ktor:ktor-server-host-common-jvm:$ktor_version"
    api "io.ktor:ktor-server-compression-jvm:$ktor_version"
    api "io.ktor:ktor-server-call-logging-jvm:$ktor_version"
    api "io.ktor:ktor-server-websockets-jvm:$ktor_version"
    api "ch.qos.logback:logback-classic:1.2.11"
    testImplementation 'junit:junit:4.13.2'
}

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8" // "17"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8" // "17"
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}


publishing {
    publications {
        mavenKotlin(MavenPublication) {
            groupId = "com.github.isyscore"
            artifactId = "common-ktor"
            version = "2.2.1"
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "common-ktor"
                description = "iSysCore Common Kotlin/Ktor Library"
                url = "https://github.com/isyscore/common-ktor"
                packaging = "jar"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/isyscore/common-ktor/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "isyscore"
                        name = "isyscore"
                        email = "hexj@isyscore.com"
                    }
                }
                scm {
                    connection = "https://github.com/isyscore/common-ktor"
                    developerConnection =  "https://github.com/isyscore/common-ktor"
                    url = "https://github.com/isyscore/common-ktor"
                }
            }
        }
    }
    repositories {
        maven {
            name = "Release"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = "$nexus_username"
                password = "$nexus_password"
            }
        }
        maven {
            name = "Snapshot"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = "$nexus_username"
                password = "$nexus_password"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenKotlin
}
